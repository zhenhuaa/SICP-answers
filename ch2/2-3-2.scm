#! /usr/bin/env csi -s

(define (deriv exp var)
  (cond ((number? exp) 0)
        ((variable? exp)
         (if (same-variable? exp var) 1 0))
        ((sum? exp)
         (make-sum (deriv (addend exp) var)
                   (deriv (augend exp) var)))
        ((product? exp)
         (make-sum (make-product (multiplier exp)  (deriv (multiplicand exp) var))
                   (make-product (multiplicand exp) (deriv (multiplier exp) var))))
        (else (error "unknown expression type -- DERIV" exp))))

(define (variable? x) (symbol? x))
(define (same-variable? v1 v2) (and (variable? v1) (variable? v2) (eq? v1 v2)))
(define (sum? x) (and (pair? x) (eq? (car x) '+)))
(define (product? x) (and (pair? x) (eq? (car x) '*)))
(define (make-sum x y) (list '+ x y))
(define (make-product x y) (list '* x y))
(define (addend x) (cadr x))
(define (augend x) (caddr x))
(define (multiplier x) (cadr x))
(define (multiplicand x) (caddr x))

(use test)
(test '(+ 1 0) (deriv '(+ x 3) 'x))
(test '(+ (* x 0) (* y 1)) (deriv '(* x y) 'x))
; (display (deriv '(* (* x y) (+ x 3)) 'x))


(define (=number? exp num)
  (and (number? exp) (= exp num)))

(define (make-sum a1 a2)
  (cond ((=number? a1 0) a2)
        ((=number? a2 0) a1)
        ((and (number? a1) (number? a2)) (+ a1 a2))
        (else (list '+ a1 a2))))

(define (make-product m1 m2)
  (cond ((or (=number? m1 0) (=number? m2 0)) 0)
        ((=number? m1 1) m2)
        ((=number? m2 1) m1)
        ((and (=number? m1 1) (=number? m2 1)) (* m1 m2))
        (else (list '* m1 m2))))

(use test)
(test 1 (deriv '(+ x 3) 'x))
(test 'y (deriv '(* x y) 'x))
